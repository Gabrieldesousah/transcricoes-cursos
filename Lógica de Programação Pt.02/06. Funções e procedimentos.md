# Funções e procedimentos

Em primeiro lugar vamos falar sobre os conceitos para depois partirmos para a prática e as diferenças.

Uma **função ** é um instrumento estático com o objetivo de retornar uma ou mais informações. Geralmente podem, ou não, receber dados de entrada para serem processados internamente, ou simplesmente ter processamentos independentes de parâmetros de entrada. O importante é entender que geralmente temos um retorno de algum dado ou alguma impressão qualquer.

Veja a estrutura de uma função.

```
algoritmo "semnome"
// Função :
// Autor :
// Data : 17/03/2017
// Seção de Declarações
var

funcao NAME\_FUNCTION ( PARAMETRO : TYPE ) : TYPE_FUNCTION
    inicio
fimfuncao

inicio

fimalgoritmo
```

Percebam que temos a função declarada fora do corpo do nosso programa. Dentro do bloco da função nós temos mais um bloco **inicio**.

Agora, sabendo a estrutura de uma função, criaremos a primeira função e já a chamaremos em nosso corpo do programa, para você ver funcionando corretamente.

```
algoritmo "semnome"
// Função :
// Autor :
// Data : 17/03/2017
// Seção de Declarações
var

funcao olaFuncao () : caractere
    inicio
    retorne "Olá dentro da função"
fimfuncao

inicio

escreval(olaFuncao())

fimalgoritmo
```

Reparem que, em nossa função real, nós definimos o tipo **caractere**, então ela deverá retornar um caractere e não um número inteiro. Nós colocamos o nome e não passamos nenhum parâmetro dentro dos parênteses, porque o parâmetro é opcional em funções.

Depois, no corpo da função, nós apenas imprimimos uma frase.

Uma função deve sermpre ter, em seu corpo, códigos que são reaproveitados várias vezes em seu algorítmo. Isso implica em vários padrões de desenvolvimento que você estudará mais pra frente, quando for estudar outras linguagens, por enquanto, coloque em sua cabeça que uma função não deve ser criada à toa, ela deve ser criada para suprir uma atividade que é repetitiva. Então uma função deve sempre ser criada para ser chamada várias vezes em seu código.

Agora que já montamos um exemplo funcional de função, fica muito fácil agora pensarmos na ideia da função que calcula o IMC.

```
algoritmo "semnome"
// Função :
// Autor :
// Data : 17/03/2017
// Seção de Declarações
var
m,h,imc : real

funcao calcImc (m,h: real) : real
var
   imc: real
inicio
   imc <- m / (h * h)
   retorne imc
fimfuncao

inicio
escreval("Massa: ")
leia(m)
escreval("Altura: ")
leia(h)
imc <- calcImc(m,h)

se imc < 17 entao
   escreval("Abaixo do peso")
senao
     se imc > 18 entao
        escreval("OK")
     fimse
fimse

fimalgoritmo
```

Em nosso exemplo de cálculo de IMC nós pegamos os dados do usuário e passamos como parâmetro para a nossa função **calcImc**. Esta função retorna sempre um valor real.

O que você deve perceber é que a função é um algorítmo a parte em relação ao nosso programa principal, tanto é que dentro de uma função existem as próprias declarações de variáveis e o próprio corpo da função. Deste modo podemos ter duas variáveis com o mesmo nome, desde que estejam em contextos diferentes.

Isso é provado porque utilizamos **m e h** para capturar dados do usuário e também utilizaos **m e h** dentro da função para calcular o IMC.

Resumindo o que fizemos podemos falar que pegamos os dados do usuário e chamamos a função que, por sua vez, processou e retornou o valor do IMC para a variável **imc**. Depois disso fizemos uma condicional para imprimir o status do resultado para o usuário.

Nós estamos adicionando todos os conceitos aprendidos até agora, para que vocês possam ver como tudo se encaixa e como tudo se relaciona. O importante é entender que o problema final está sendo resolvido, que é mostra o status do Índice de Massa Corpórea para o usuário final.

E para isso estamos utilizando um função que calcula o imc. Esta função eu posso utilizar quantas vezes eu quiser e em quam projeto eu quiser, se eu quiser levar para outro programa eu posso , porque ela estará pronta e eu só copio e colo em outro projeto. Esta é o real objetivo de uma função: reaproveitamento de código.

### Procedimentos

Procedimento também é um subalgoritmo, mas tem uma pequena diferença em relação a função, porém, apesar de pequena a diferença estrutural, quando falamos de resolução de problemas a diferença se torna muito grande.

A função retorna sempre algum valor, de acordo com o tipo da função declarada. Até este ponto já entendemos e já praticamos.

Agora o procedimento vai ter a mesma estrutura de uma função, porém ele não retornará um valor.

```
algoritmo "semnome"
// Função :
// Autor :
// Data : 17/03/2017
// Seção de Declarações
var
num1, num2 : inteiro

procedimento troca (x, y : inteiro)
var
    a: inteiro
inicio
    a <- x
    x <- y
    y <- a
    escreval(x, y)
fimprocedimento

inicio
escreval("Número 1: ")
leia(num1)
escreval("Número 2: ")
leia(num2)

troca(num1, num2)

fimalgoritmo
```

Reparem que o nosso procedimento apenas inverte os valores informados, colocamos o valor de **y** em **x** e o de **x** em **y**. Para isso utilizamos a variável **a** para salvar o valor de **x**, pois quando atribuímos o valor de **y** em **x** não sobrescrevemos o valor. Se não tivéssemos salvo em outra variável nós o perderíamos. Depois só jogamos o valor de **a** para a variável **y**.

Reparem que quando chamamos o procedimento, no corpo do nosso programa, ele já se encarrega de imprimir o valor, ou seja, um procedimento já executa o seu propósito. Sendo que a função retornaria um valor e depois nós precisaríamos tratá-lo ou imprimí-lo.

Tirando a lógica do algorítmo, o importante é você entender esta diferença entre função e procedimento. A função retorna um valor e o procedimento executa o seu objetivo sem retornar nada.

Com procedimento você pode executar qualquer ação que não necessite retornar um valor para manipulação dentro do seu programa, inclusive menus de interação com usuários, desde que aquela ação seja concluída no próprio procedimento.

Resumindo, a maior diferença entre função e procedimento é que a função retorna um valor que pode, ou não, ser manipulado e o procedimento apenas executa um código a parte sem retornar nada para o programa manipular, ou seja, o procedimento tem inicio e fim localmente, sem retornar absolutamente nada.