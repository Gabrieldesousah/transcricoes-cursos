# Funções e procedimentos

Primeiro, falaremos sobre os conceitos para depois partirmos para a prática e as diferenças.

Uma **função ** é um instrumento estático com o objetivo de retornar uma ou mais informações. Geralmente, podem ou não, receber dados de entrada para serem processados, internamente, ou ter processamentos independentes de parâmetros de entrada. O importante é entenderem que, geralmente, temos um retorno de algum dado ou alguma impressão qualquer.

Vejam a estrutura de uma função.

```
algoritmo "semnome"
// Função :
// Autor :
// Data : 17/03/2017
// Seção de Declarações
var

funcao NAME\_FUNCTION ( PARAMETRO : TYPE ) : TYPE_FUNCTION
    inicio
fimfuncao

inicio

fimalgoritmo
```

Observem que temos a função declarada, fora do corpo do programa. Dentro do bloco da função, temos mais um bloco **inicio**.

Conhecendo a estrutura da função, criaremos a primeira função e a chamaremos, no corpo do programa, para verem funcionando, corretamente.

```
algoritmo "semnome"
// Função :
// Autor :
// Data : 17/03/2017
// Seção de Declarações
var

funcao olaFuncao () : caractere
    inicio
    retorne "Olá dentro da função"
fimfuncao

inicio

escreval(olaFuncao())

fimalgoritmo
```

Notem que, na função real, definimos o tipo **caractere**. Ela deverá retornar um caractere e não um número inteiro. Colocamos o nome e não passamos nenhum parâmetro dentro dos parênteses, porque o parâmetro é opcional, em funções.

Em seguida, no corpo da função, imprimimos uma frase.

Uma função deve, sempre, ter em seu corpo, códigos que são reaproveitados, várias vezes, no algorítmo. Isso implica em vários padrões de desenvolvimento que vocês estudarão, adiante, quando forem estudar outras linguagens. Por enquanto, entendam que uma função não deve ser criada à toa, ela deve ser criada para suprir uma atividade que é repetitiva. Uma função deve ser criada para ser chamada, várias vezes, no código.

Já montamos um exemplo funcional de função, ficará muito fácil pensarmos na idéia da função que calcula o IMC.

```
algoritmo "semnome"
// Função :
// Autor :
// Data : 17/03/2017
// Seção de Declarações
var
m,h,imc : real

funcao calcImc (m,h: real) : real
var
   imc: real
inicio
   imc <- m / (h * h)
   retorne imc
fimfuncao

inicio
escreval("Massa: ")
leia(m)
escreval("Altura: ")
leia(h)
imc <- calcImc(m,h)

se imc < 17 entao
   escreval("Abaixo do peso")
senao
     se imc > 18 entao
        escreval("OK")
     fimse
fimse

fimalgoritmo
```

No exemplo de cálculo de IMC, pegamos os dados do usuário e passamos como parâmetro para a função **calcImc**. Esta função retorna, sempre, um valor real.

O que vocês devem observar é que, a função é um algorítmo à parte, em relação ao programa principal, tanto é que, dentro de uma função existem as próprias declarações de variáveis e o próprio corpo da função. Deste modo, podemos ter duas variáveis com o mesmo nome, desde que estejam em contextos diferentes.

Isso é provado no código para cáculo do IMC, através da utilização do **m e h** para capturar dados do usuário e também utilizamos **m e h** dentro da função, para calcular o IMC.

Resumindo o que fizemos, podemos falar que pegamos os dados do usuário e chamamos a função que, por sua vez, processou e retornou o valor do IMC para a variável **imc**. Em seguida, fizemos uma condicional para imprimir o status do resultado para o usuário.

Adicionamos todos os conceitos aprendidos até agora, para que vocês possam ver como tudo se encaixa e se relaciona. O importante é entenderem que o problema final está sendo resolvido, que é mostrar o status do Índice de Massa Corpórea para o usuário final.

Para isso, utilizamos um função que calcula o imc. Esta função, podemos utilizar quantas vezes quisermos e em quantos projetos quisermos. Se quisermos levar para outro programa podemos, porque ela estará pronta e só copiamos e colamos em outro projeto. Este é o real objetivo de uma função: reaproveitamento de código.

### Procedimentos

Procedimento, também, é um subalgoritmo, mas tem uma pequena diferença em relação a função, apesar de pequena, é uma diferença estrutural. Quando falamos de resolução de problemas, a diferença se torna muito grande.

A função retorna sempre algum valor, de acordo com o tipo da função declarada. Até este ponto, já entendemos e praticamos.

Agora, o procedimento terá a mesma estrutura de uma função, porém ele não retornará um valor.

```
algoritmo "semnome"
// Função :
// Autor :
// Data : 17/03/2017
// Seção de Declarações
var
num1, num2 : inteiro

procedimento troca (x, y : inteiro)
var
    a: inteiro
inicio
    a <- x
    x <- y
    y <- a
    escreval(x, y)
fimprocedimento

inicio
escreval("Número 1: ")
leia(num1)
escreval("Número 2: ")
leia(num2)

troca(num1, num2)

fimalgoritmo
```

Observem que o procedimento, apenas, inverte os valores informados, colocamos o valor de **y** em **x** e o de **x** em **y**. Para isso, utilizamos a variável **a** para salvar o valor de **x**, pois quando atribuímos o valor de **y** em **x**, não sobrescrevemos o valor. Se não tivéssemos salvo, em outra variável, nós o perderíamos. Em seguida, só jogamos o valor de **a** para a variável **y**.

Notem que, quando chamamos o procedimento, no corpo do programa, ele se encarrega de imprimir o valor, ou seja, um procedimento já executa o seu propósito. Sendo que, a função retornaria um valor e depois precisaríamos tratá-la ou imprimí-la.

Tirando a lógica do algorítmo, o importante é entenderem esta diferença entre função e procedimento. A função retorna um valor e o procedimento executa o seu objetivo, sem retornar nada.

Com procedimento, vocês podem executar qualquer ação que não necessite retornar um valor, para manipulação dentro do programa, inclusive, menus de interação com usuários, desde que aquela ação seja concluída no próprio procedimento.

Resumindo, a maior diferença entre função e procedimento é que a função retorna um valor que, pode ou não, ser manipulado e o procedimento, apenas, executa um código, à parte, sem retornar nada para o programa manipular, ou seja, o procedimento tem inicio e fim, localmente, sem retornar, absolutamente, nada.