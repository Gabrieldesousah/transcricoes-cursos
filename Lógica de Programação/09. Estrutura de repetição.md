# Estrutura de repetição

Falaremos sobre estruturas de repetição neste módulo e, como os últimos dois módulos, é um assunto muito importante e muito utilizado na programação.

Quando desejamos que um determinado conjunto de instruções sejam executados uma quantidade determinada de vezes, nós conseguimos utilizar as estruturas de repetições.

Existem dois tipos de estrutura de repetição: **while / enquanto** e **for / para**. Ambas as estruturas trabalham com o mesmo princípio que é executar uma operação até que uma lógica, pré-determinada, seja verdadeira. Isso quer dizer que no primeiro instante em que esta lógica for falsa a estrutura para de repetir. Então devemos sempre ter muito cuidado de nos atentar para ver se esta estrutura realmente terá uma expressão falsa, caso seja sempre verdadeira nós caímos em um erro, chamado loop infinito, que pode travar a máquina de quem está rodando o programa.

Estas estruturas também geram um desvio em nosso código, porque ela repetirá, algumas vezes, determinado bloco antes de continuar o fluxo de processamento do programa.

### enquanto / while

```
algoritmo "semnome"
// Função :
// Autor :
// Data : 02/27/2017
// Seção de Declarações
var
parar: logico
continuar: caractere

inicio
parar <- verdadeiro

enquanto parar faca
    escreval("Bem vindo ao meu menu")
    escreval("Deseja continuar (S?N)?")
    leia(continuar)

    se (continuar = "N") ou (continuar = "n") entao
        parar = falso
    fimse
fimenquanto

fimalgoritmo
```

Analisaremos o código acima para que você entenda corretamente. Em primeiro lugar devemos nos atentar à estrutura do enquanto.

```
enquanto parar faca

fimenquanto
```

Esta é a estrutura do enquanto, que você sempre encontrará. O que vem dentro dela é o bloco de repetição.

Percebam que, na primeira linha, temos uma expressão e é ela que determina quando a repetição acaba. A expressão **enquanto parar faca** é a mesma coisa que **enquanto (parar = verdadeiro) faca**.

Como nós definimos a variável parar como verdadeira, antes da repetição, esta condição já é verdadeira então entramos no nosso bloco de repetição. Este bloco simplesmente mostra uma mensagem e pergunta se quer continuar no menu, caso o usuário digite "s" ou "S" não acontece nada e se não acontece nada o nosso parar continua verdadeiro, então a nossa estrutura também continua se repetindo.

Quando o usuário digitar "n" ou "N" a nossa estrutura condicional será verdadeira então, além do bloco do enquanto ser executado, teremos o bloco de condicional sendo executado também. Neste caso ele entra dendo do bloco e altera o valor da variável parar para falso.

Quando a repetição for ser iniciada novamente ele vai testar se a variável é verdadeira, mas agora não será mais, porque o próprio bloco a alterou, então temos o fim de nossa repetição.

Percebam que se não criássemos uma lógica para que a variável parar fosse modificada para false, nós teríamos um loop infinito, porque o valor da mesma sempre seria verdadeiro. Então fiquem sempre atentos, porque um loop sempre deve ter um fim. A lógica é você quem determina, mas fiquem atentos.

Neste exemplo estamos utilizando o enquanto com uma variável do tipo lógica, mas poderia ser um inteiro, por exemplo. Onde poderíamos setar um valor inicial e testar, em nossa expressão, um valor final. Então imagina que nós começamos uma variável x com o valor 1 e queremos que a repetição seja executada até que esta mesma variável seja menor do que 10. Lógico que teremos que incrementar este valor, dentro do bloco, mas seria uma outra forma de executar a repetição com "enquanto".

Você verá um exemplo de estrutura de repetição com contador numérico abaixo.

### for / para

```
algoritmo "semnome"
// Função :
// Autor :
// Data : 02/27/2017
// Seção de Declarações
var
j: inteiro

inicio

para j de 10 ate 1 passo -1 faca
    escreval(j)
fimpara

fimalgoritmo
```

Se você executar este programa no VisuAlg, terá o seguinte resultado:

```
10
9
8
7
6
5
4
3
2
1

*** Fim da execução.
*** Feche esta janela para retornar ao Visualg.
```

Percebam que, do mesmo modo que o *enquanto*, o que controla a repetição é a primeira linha, onde está nossa expressão. Enquanto a expressão for verdadeira ela continuará executando o bloco de repetição.

Nós iniciamos a variável j com o valor 10. Então se analisarmos a expressão teremos o primeiro teste sendo positivo, o que significa entrar na repetição.

Quando interpretamos a expressão **para j de 10 ate 1** estamos falando que a repetição deverá ser executada se **(j <= 10)** ou **(j >=1)**. Então se j for qualquer valor entre 10 e 1 a expressão será verdadeira e consequentemente a repetição será executada.

Para que a repetição tenha um fim nós adicionamos o parâmetro **passo**. Este parâmetro serve para modificar o valor de j, dentro do bloco de repetição. Se não tivéssemos este decremento no valor, nós teríamos mais um caso de loop infinito, porque a expressão sempre seria verdadeira, uma vez que o valor de j sempre seria 10.

Então resumindo o processo podemos dizer que a estrutura analisa o valor de j, compara se está entre 10 e 1, como o valor é 10 a expressão retorna verdadeiro e executa o bloco. Quando este bloco é executado o **passo** já subtrai 1 da variável j, que agora passa a ser 9 e não mais 10. E o bloco é executado imprimindo o valor 10.

A expressão volta a ser analisada e agora o valor de j é 9, mas ainda sim se encontra entre 10 e 1, portanto, expressão verdadeira, execução do bloco e subtração da variável j, que passa a ser 8.

Este processo é executado até que a variável j seja 0, porque ai a expressão retornará falso, pois 0 não está entre 10 e 1. Então temos o fim da nossa estrutura de repetição.

### Comparações

Se compararmos as duas estruturas podemos ver uma diferença, que é muito importante, na expressão. No *enquanto* o decremento, ou incremento, do contador deve ser feito dentro do bloco de repetição e o valor inicial deve ser informado antes da repetição. Já na estrutura for é feito tudo em apenas uma linha, junto com a expressão. Também temos a palavra reservada **passo**, que no **para** faz o incremento, ou decremento, automaticamente, já no *enquanto* nós teríamos que fazer da seguinte forma:

`j <- j -1`

Vejam exemplos práticos de duas estruturas que são diferentes, mas emitem o mesmo resultado. Faremos uma estrutura *para* e uma *enquanto* com a mesma finalidade.

```
algoritmo "semnome"
// Função :
// Autor :
// Data : 16/03/2017
// Seção de Declarações
var
j: inteiro

inicio

escreval("Imprimindo de 1 a 10 com estrutura para")
j <- 1
para j de 1 ate 10 passo 1 faca
       escreval(j)
fimpara

escreval("Imprimindo de 1 a 10 com estrutura enquanto")
j <- 1
enquanto j <= 10 faca
    escreval(j)
    j <- j + 1
fimenquanto

fimalgoritmo
```

Analisem as diferenças e você poderá concluir que a estrura *para* é mais automatizada e a *enquanto* é mais manual, mas as duas trabalham praticamente da mesma maneira.

O conceito é o mesmo, que é fornecer uma estrutura de repetição controlada e facilitar tarefas que são repetitivas.